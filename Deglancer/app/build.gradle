apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'

retrolambda {
    jdk System.getenv("JAVA_HOME")
    oldJdk System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
    defaultMethods false
    incremental true
}
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "org.coderswithoutborders.deglancer"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 5
        versionName "1.0.4"
    }
    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {

    final JUNIT_VERSION = '4.12'
    final SUPPORT_LIBRARY_VERSION = '23.3.0'
    final RX_ANDROID_VERSION = '1.1.0'
    final RX_JAVA_VERSION = '1.1.0'
    final DAGGER_VERSION = '2.0'
    final DAGGER_COMPILER_VERSION = '2.0'
    final ANNOTATION_VERSION = '1.0'
    final FIREBASE_VERSION = '9.0.2'
    final RX_PREFERENCES_VERSION = '1.0.1'
    final JODA_TIME_VERSION = '2.9.3'
    final ACRA_VERSION = '4.6.2'

    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:percent:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"

    compile "com.google.firebase:firebase-database:9.0.2"

    compile 'com.google.firebase:firebase-auth:9.0.2'
    compile 'com.google.firebase:firebase-core:9.0.2'

    compile "com.f2prateek.rx.preferences:rx-preferences:$RX_PREFERENCES_VERSION"

    compile "io.reactivex:rxandroid:$RX_ANDROID_VERSION"
    compile "io.reactivex:rxjava:$RX_JAVA_VERSION"

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    apt "com.google.dagger:dagger-compiler:$DAGGER_COMPILER_VERSION"
    provided "javax.annotation:jsr250-api:$ANNOTATION_VERSION"

    compile "joda-time:joda-time:$JODA_TIME_VERSION"
    compile "ch.acra:acra:$ACRA_VERSION"

    compile 'com.jakewharton.timber:timber:4.1.2'

    compile 'com.google.code.gson:gson:2.4'

    testCompile "junit:junit:$JUNIT_VERSION"
}

task switchToDebug(type: Copy) {
    description = 'Switches to DEBUG google-services.json'
    from "src/debug"
    include "google-services.json"
    into "."
}

task switchToRelease(type: Copy) {
    description = 'Switches to RELEASE google-services.json'
    from "src/release"
    include "google-services.json"
    into "."
}

afterEvaluate {
    processDebugGoogleServices.dependsOn switchToDebug
    processReleaseGoogleServices.dependsOn switchToRelease
}

apply plugin: 'com.google.gms.google-services'
